apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "coding_trip.io.android"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        // GitHub認証のidとsecretキーをロードする
        // idとキーはGitHubで共有していない
        def CLIENT_ID
        def CLIENT_SECRET

        def file = project.file("gradle.properties")
        if (file.exists()) {
            def properties = new Properties()
            properties.load(file.newDataInputStream())
            CLIENT_ID = properties.get("CLIENT_ID")
            CLIENT_SECRET = properties.get("CLIENT_SECRET")
        } else {
            CLIENT_ID = System.getenv("CLIENT_ID")
            CLIENT_SECRET = System.getenv("CLIENT_SECRET")
        }

        buildConfigField 'String', 'CLIENT_ID', "$CLIENT_ID"
        buildConfigField 'String', 'CLIENT_SECRET', "$CLIENT_SECRET"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    support_lib_version = '27.0.2'
    constraint_layout_version = '1.1.0-beta5'

    ktx_version = '0.1'

    rxkotlin_version = '2.2.0'

    dagger_version = '2.14.1'

    retrofit_version = '2.3.0'
    okhttp_version = '3.9.1'
    moshi_version = '1.5.0'

    timber_version = '4.6.0'
    firebase_version = '11.8.0'
    glide_version = '4.5.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // core
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:recyclerview-v7:$support_lib_version"
    implementation "com.android.support:cardview-v7:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"
    implementation "com.android.support:customtabs:$support_lib_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    implementation "androidx.core:core-ktx:$ktx_version"

    // rx
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"
    implementation "com.trello.rxlifecycle2:rxlifecycle-android-lifecycle-kotlin:$rxkotlin_version"

    // dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // api
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    // log
    implementation "com.jakewharton.timber:timber:$timber_version"

    // firebase
    implementation "com.google.firebase:firebase-core:$firebase_version"
    implementation "com.google.firebase:firebase-auth:$firebase_version"

    // glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // test
    testImplementation "junit:junit:4.12"
    androidTestImplementation "com.android.support.test:runner:1.0.1"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.1"
}

apply plugin: 'com.google.gms.google-services'
